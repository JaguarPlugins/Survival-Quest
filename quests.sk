on load:
    set {quest.chal.messages::pick} to "Craft a diamond pickaxe"
    set {quest.chal.rewards::pick} to 5

    set {quest.chal.messages::farm} to "Craft a hoe"
    set {quest.chal.rewards::farm} to 3

function questInit(w: world):
    loop {quest.maps::%{_w}%::players::*}:
        clear {quest.%loop-value%.challenges::*}
        delete {quest.%loop-value%.chal}

        add "pick" to {quest.%loop-value%.challenges::*}
        add "farm" to {quest.%loop-value%.challenges::*}

        newQuest(loop-value)

function newQuest(p: player):
    if {quest.%{_p}%.chal} is set:

        set {_chal} to {quest.%{_p}%.chal}
        set {_num} to {quest.chal.rewards::%{_chal}%}

        loop {_num} times:    
            give {_p} an emerald
        send title "&AChallenge complete!" to {_p} for 3 seconds

        remove {quest.%{_p}%.chal} from {quest.%{_p}%.challenges::*}
        delete {quest.%{_p}%.chal}

    if size of {quest.%{_p}%.challenges::*} > 0:

        set {quest.%{_p}%.challenges::*} to {quest.%{_p}%.challenges::*}
        set {_i} to a random integer between 1 and (size of {quest.%{_p}%.challenges::*})
        set {quest.%{_p}%.chal} to {quest.%{_p}%.challenges::%{_i}%}
        set {_chal} to {quest.%{_p}%.chal}
        # set action bar of {_p} to {quest.chal.messages::%{_chal}%}
        send "%{quest.chal.messages::%{_chal}%}%" to {_p}

    else:

        wait 1 second
        send title "&AAll quests complete!" to {_p} for 3 seconds
    wait 3 seconds
    reset title of {_p}

# Triggers
on craft of diamond pickaxe:
    if {quest.maps::%player's world%::players::*} contains player:
        if {quest.%player%.chal} is "pick":
            newQuest(player)

on craft of wooden hoe or stone hoe or iron hoe or gold hoe or diamond hoe or netherite hoe:
    if {quest.maps::%player's world%::players::*} contains player:
        if {quest.%player%.chal} is "farm":
            newQuest(player)